// We must identify the kernal (entry-point) functions.
#pragma kernel compute_shader_main

#include "swarm_shader_types.cginc"

uniform RWStructuredBuffer<s_swarmer_state> u_inout_swarmers;
uniform int u_swarmer_count;

uniform StructuredBuffer<s_attractor_state> u_attractors;
uniform int u_attractor_count;

uniform float u_delta_time;

static const float k_ideal_velocity_blending_rate = 10.0f;

static const float k_swarmer_speed_min = 0.1f;
static const float k_swarmer_speed_idle = 0.5f;
static const float k_swarmer_speed_max = 1.0f;

static const float k_neighborhood_radius = 0.3f;

static const float k_neighbor_attraction_scalar = 0.1f;
static const float k_neighbor_collision_avoidance_scalar = 10.0f;
static const float k_neighbor_alignment_scalar = 0.1f;

float3 build_attractors_impulse(
	uint swarmer_index)
{
	float3 result = (float3)0.0f;

	for (int attractor_index = 0; 
		attractor_index < u_attractor_count; 
		++attractor_index)
	{
		float3 swarmer_to_attractor =
			(u_attractors[attractor_index].position - u_inout_swarmers[swarmer_index].position);

		float distance = length(swarmer_to_attractor);

		float effect_fraction = (
			1.0f -
			smoothstep(
				u_attractors[attractor_index].falloff_inner_radius,
				u_attractors[attractor_index].falloff_outer_radius,
				distance));

		result += (
			effect_fraction * 
			u_attractors[attractor_index].attraction_scalar *
			(swarmer_to_attractor / distance));

		result += (
			effect_fraction * 
			u_attractors[attractor_index].thrust_scalar *
			u_attractors[attractor_index].thrust_direction);
	}

	return result;
}

float3 build_flocking_impulse(
	uint swarmer_index)
{
	float neighbor_count = 0.0001f;
	float3 attraction_summation = (float3)0.0f;
	float3 collision_avoidance_summation = (float3)0.0f;
	float3 alignment_summation = (float3)0.0f;

	// Factor in the other boids.
	for (int neighbor_index = 0; neighbor_index < u_swarmer_count; ++neighbor_index)
	{
		float3 swarmer_to_neighbor =
			(u_inout_swarmers[neighbor_index].position - u_inout_swarmers[swarmer_index].position);

		float distance = max(0.001, length(swarmer_to_neighbor)); // Minimum-distance avoids a potential divide-by-zero.

		float3 swarmer_to_neighbor_direction = (swarmer_to_neighbor / distance);

		float case_is_neighbor = (
			(neighbor_index != (int)swarmer_index) * // Ignore comparisons against ourselves (without disrupting branching).
			(distance < k_neighborhood_radius));

		float distance_fraction = saturate(distance / k_neighborhood_radius);
		float proximity_fraction = (1.0f - distance_fraction);

		neighbor_count += case_is_neighbor;

		attraction_summation += (
			case_is_neighbor *
			swarmer_to_neighbor_direction);

		collision_avoidance_summation += (
			case_is_neighbor *
			(proximity_fraction * proximity_fraction) *
			(-1 * swarmer_to_neighbor_direction));

		alignment_summation += (
			case_is_neighbor *
			u_inout_swarmers[neighbor_index].velocity);
	}

	float3 flocking_summation = (
		(k_neighbor_attraction_scalar * attraction_summation) +
		(k_neighbor_collision_avoidance_scalar * collision_avoidance_summation) +
		(k_neighbor_alignment_scalar * alignment_summation));

	return (flocking_summation / neighbor_count);
}

[numthreads(64, 1, 1)]
void compute_shader_main(
	uint3 thread_id : SV_DispatchThreadID)
{
	// Default to idling forwards.
	float3 idealVelocity = 
		(k_swarmer_speed_idle * normalize(u_inout_swarmers[thread_id.x].velocity));

	idealVelocity += build_attractors_impulse(thread_id.x);

	idealVelocity += build_flocking_impulse(thread_id.x);

	u_inout_swarmers[thread_id.x].velocity = 
		lerp(
			u_inout_swarmers[thread_id.x].velocity,
			idealVelocity,
			saturate(u_delta_time * k_ideal_velocity_blending_rate));
	
	u_inout_swarmers[thread_id.x].position += 
		(u_delta_time * u_inout_swarmers[thread_id.x].velocity);
	
	u_inout_swarmers[thread_id.x].local_up += float3(0.0f, 1.0f, 0.0f);
}
