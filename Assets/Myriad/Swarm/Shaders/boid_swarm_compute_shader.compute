// We must identify the kernel (entry-point) functions.
#pragma kernel kernel_advance_swarmer_states

#include "swarm_shader_types.cginc"
#include "swarm_shader_utilities.cginc"
#include "..\..\Spatializer\Shaders\spatializer_shader_types.cginc"
#include "..\..\Spatializer\Shaders\spatializer_shader_utilities.cginc"

uniform uint u_swarmer_count;
uniform StructuredBuffer<s_swarmer_state> u_readable_swarmers;
uniform RWStructuredBuffer<s_swarmer_state> u_out_next_swarmers;

uniform uint u_voxel_count_per_axis;
uniform StructuredBuffer<s_voxel_particle_pair> u_spatialization_voxel_particle_pairs;
uniform StructuredBuffer<s_spatialization_voxel> u_spatialization_voxels;
uniform StructuredBuffer<s_neighborhood> u_spatialization_neighborhoods;

uniform uint u_forcefield_count;
uniform StructuredBuffer<s_forcefield_state> u_forcefields;

uniform float u_neighborhood_radius;
uniform uint u_max_neighbor_count;

uniform float u_delta_time;

static const float k_ideal_velocity_blending_rate = 10.0f;

static const float k_swarmer_speed_min = 0.1f;
static const float k_swarmer_speed_idle = 0.5f;
static const float k_swarmer_speed_max = 1.0f;

static const float k_neighbor_attraction_scalar = 0.1f;
static const float k_neighbor_collision_avoidance_scalar = 10.0f;
static const float k_neighbor_alignment_scalar = 0.1f;

static const float k_swarmer_model_scale= 0.04f;

float3 build_forcefields_impulse(
	float3 swarmer_position)
{
	float3 result = (float3)0.0f;

	for (uint forcefield_index = 0; 
		forcefield_index < u_forcefield_count; 
		++forcefield_index)
	{
		s_forcefield_state forcefield = u_forcefields[forcefield_index];

		float3 swarmer_in_forcefield_space = mul(
			forcefield.simulation_to_forcefield_matrix, 
			float4(swarmer_position, 1.0f)).xyz;

		float distance_to_field_core;
		float3 field_force_direction;

		switch (forcefield.forcefield_type)
		{
			case k_forcefield_type_global:
				distance_to_field_core = 0.0f;
				field_force_direction = float3(0.0f, 1.0f, 0.0f);
				break;

			case k_forcefield_type_plane:
				distance_to_field_core = max(0.0f, swarmer_in_forcefield_space.y);
				field_force_direction = float3(0.0f, 1.0f, 0.0f);
				break;

			case k_forcefield_type_sphere:
				distance_to_field_core = length(swarmer_in_forcefield_space);
				field_force_direction = (swarmer_in_forcefield_space / distance_to_field_core);
				break;

			case k_forcefield_type_thrust_capsule:
				distance_to_field_core =
					distance(
						float3(0.0f, clamp(swarmer_in_forcefield_space.y, 0.0f, forcefield.forcefield_length), 0.0f),
						swarmer_in_forcefield_space);
				field_force_direction = float3(0.0f, 1.0f, 0.0f);
				break;

			default:
				distance_to_field_core = 0.0f;
				field_force_direction = float3(0.0f, 0.0f, 0.0f);
				break;
		}

		field_force_direction = 
			mul(
				forcefield.forcefield_to_simulation_matrix, 
				float4(field_force_direction, 0.0f)).xyz;

		float effect_fraction = (
			1.0f -
			smoothstep(
				forcefield.falloff_inner_radius,
				forcefield.falloff_outer_radius,
				distance_to_field_core));

		result += (
			(effect_fraction * forcefield.force_scalar) * 
			field_force_direction);
	}

	return result;
}

float3 build_flocking_impulse(
	uint swarmer_index,
	float3 swarmer_position)
{
	float neighbor_count = 0.0f;
	float3 attraction_summation = (float3)0.0f;
	float3 collision_avoidance_summation = (float3)0.0f;
	float3 alignment_summation = (float3)0.0f;

	uint3 neighborhood_min_voxel_coord = u_spatialization_neighborhoods[swarmer_index].neighborhood_min_voxel_coord;

	// Start the neighborhood enumeration by forcing the first candidate to be a guaranteed-failure.
	int neighborhood_enumeration_index = 0;
	uint neighborhood_voxel_index = (uint)-1;
	uint candidate_voxel_particle_pair_index = u_swarmer_count; // Guaranteed to fail.
	uint processed_candidate_count = 0;

	// Until the enumeration internally-terminates, keep processing neighbors.
	while (processed_candidate_count < u_max_neighbor_count)
	{
		// While the candidate-particle fails to reside in the current voxel, advance the neighborhood-voxel enumeration.
		// NOTE: There's internal-termination for when the entire enumeration process completes.
		while ((candidate_voxel_particle_pair_index >= u_swarmer_count) ||
			(u_spatialization_voxel_particle_pairs[candidate_voxel_particle_pair_index].voxel_index != neighborhood_voxel_index))
		{
			neighborhood_voxel_index =
				spatialization_get_neighborhood_enumeration_voxel_index(
					neighborhood_min_voxel_coord,
					u_voxel_count_per_axis,
					/*inout*/ neighborhood_enumeration_index);
			
			// If the enumeration has completed, bail out.
			if (neighborhood_enumeration_index == -1)
			{
				break;
			}

			candidate_voxel_particle_pair_index =
				u_spatialization_voxels[neighborhood_voxel_index].first_voxel_particle_pair_index;
		}
		
		// If the enumeration has completed, bail out.
		if (neighborhood_enumeration_index == -1)
		{
			break;
		}

		uint neighbor_index = 
			u_spatialization_voxel_particle_pairs[candidate_voxel_particle_pair_index].particle_index;
		
		// Process the potential-neighbor.
		{
			float3 swarmer_to_neighbor =
				(u_readable_swarmers[neighbor_index].position - swarmer_position);

			float distance = max(0.001, length(swarmer_to_neighbor)); // Minimum-distance avoids a potential divide-by-zero.

			float3 swarmer_to_neighbor_direction = (swarmer_to_neighbor / distance);

			float case_is_neighbor = (
				(neighbor_index != swarmer_index) * // Ignore comparisons against ourselves (without disrupting branching).
				(distance < u_neighborhood_radius));

			float distance_fraction = saturate(distance / u_neighborhood_radius);
			float proximity_fraction = (1.0f - distance_fraction);

			neighbor_count += case_is_neighbor;

			attraction_summation += (
				case_is_neighbor *
				swarmer_to_neighbor_direction);

			collision_avoidance_summation += (
				case_is_neighbor *
				(proximity_fraction * proximity_fraction) *
				(-1 * swarmer_to_neighbor_direction));

			alignment_summation += (
				case_is_neighbor *
				u_readable_swarmers[neighbor_index].velocity);
		}

		// Advance to the next candidate-particle.
		++candidate_voxel_particle_pair_index;
		++processed_candidate_count;
	}

	float3 flocking_summation = (
		(k_neighbor_attraction_scalar * attraction_summation) +
		(k_neighbor_collision_avoidance_scalar * collision_avoidance_summation) +
		(k_neighbor_alignment_scalar * alignment_summation));

	return (flocking_summation / max(neighbor_count, 1));
}

[numthreads(64, 1, 1)]
void kernel_advance_swarmer_states(
	uint3 thread_id : SV_DispatchThreadID)
{
	uint swarmer_index = thread_id.x;

	// In the last thread group, if it's safe to access the buffers.
	if (swarmer_index < u_swarmer_count)
	{
		s_swarmer_state new_swarmer_state = u_readable_swarmers[swarmer_index];

		// Default to idling forwards.
		float3 idealVelocity =
			(k_swarmer_speed_idle * normalize(new_swarmer_state.velocity));

		idealVelocity += build_forcefields_impulse(new_swarmer_state.position);

		idealVelocity += build_flocking_impulse(swarmer_index, new_swarmer_state.position);

		new_swarmer_state.velocity =
			lerp(
				new_swarmer_state.velocity,
				idealVelocity,
				saturate(u_delta_time * k_ideal_velocity_blending_rate));

		new_swarmer_state.position +=
			(u_delta_time * new_swarmer_state.velocity);

		new_swarmer_state.local_up = float3(0.0f, 1.0f, 0.0f); // TODO: Animate this as the boid turns left/right.

		new_swarmer_state.cached_model_to_swarm_matrix =
			build_swarmer_model_to_swarm_matrix(
				new_swarmer_state.position,
				new_swarmer_state.velocity,
				new_swarmer_state.local_up,
				k_swarmer_model_scale);

		u_out_next_swarmers[swarmer_index] = new_swarmer_state;
	}
}
